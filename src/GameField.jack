// This should probably just be a two dimensional array containing booleans
// Then we will create a graphical api that will be able to display the field properly.

class GameField {
    field int height, width;
    field Array aliveCellSprite;
    field Array deadCellSprite;
    field Array data;
    constructor GameField new(int _width, int _height) {
        var int x;
        let height = _height;
        let width = _width;
        let aliveCellSprite = Sprites.aliveCell();
        let deadCellSprite = Sprites.deadCell();
        let data = Array.new(_height * _width);
        
        let x = 0;
        while( x < (_height * _width)) {
            let data[x] = 0;
            let x = x + 1;
        }
        return this;
    }

    constructor GameField copy(GameField gameField) {
        var int x;
        var int y;

        let height = gameField.getHeight();
        let width = gameField.getWidth();
        let data = Array.new(height * width);
        
        let y = 0;
        while (y < height) {
            let x = 0;
            while (x < width) {
                let data[(y * width) + x] = gameField.isAliveAt(x, y);
                let x = x + 1;
            }
            let y = y + 1;
        }
        return this;
    }

    method int getHeight() {
        return height;
    }

    method int getWidth() {
        return width;
    }

    method void set(boolean alive, int x, int y) {
        var int position;
        if(Helpers.and(
            Helpers.and((x < width), x > -1),
            Helpers.and((y < height), y > -1))
        ) {
            let position = (y * width) + x;
            let data[position] = alive;
            if(alive) {
                do Drawer.draw(aliveCellSprite, 6, 6, x * 6, y * 6);
            } else {
                do Drawer.draw(deadCellSprite, 6, 6, x * 6, y * 6);
            }
        }
        return;
    }

    method boolean isAliveAt(int x, int y) {
        var int position;
        if(Helpers.and(
            Helpers.and((x < width), x > -1),
            Helpers.and((y < height), y > -1))
        ) {
            let position = (y * width) + x;
            return data[position];
        } else {
            return false;
        }
    } 

    //Do not redraw this all the time,
    //It is not needed.
    //Draw everytime with set instead.
    method void reDraw() {
        var int x;
        var int y;

        //do Screen.clearScreen();
        let y = 0;
        while(y < height) {
            let x = 0;
            while(x < width) {
                if(data[(y * width) + x]) {
                    do Drawer.draw(aliveCellSprite, 6, 6, x * 6, y * 6);
                } else {
                    do Drawer.draw(deadCellSprite, 6, 6, x * 6, y * 6);
                }
                let x = x + 1;
            }
            let y = y + 1;
        }
        return;
    }

    method void dispose() {
        do data.dispose();
        do Memory.deAlloc(this);
        return;
    }
}