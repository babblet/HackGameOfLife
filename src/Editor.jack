class Editor {
    function void edit(GameField gameField) {
        var Array editorCursor;
        var int x;
        var int y;
        var int _x;
        var int _y;
        var boolean toggle;
        var boolean keypressed;

        let x = 0;
        let y = 0;
        let _x = 0;
        let _y = 0;
        let toggle = false;
        let keypressed = false;
        let editorCursor = Sprites.editorCursor();

        do gameField.reDraw();
        while(~Helpers.or(Controller.quit(),
                          Controller.mode())) {
            if(Controller.switch()) {
                if (~keypressed) {
                    let toggle = true;
                } else {
                    let toggle = false;
                }
                let keypressed = true;
            } else {
                let toggle = false;
                let keypressed = false;
            }

                    
            if(Controller.left()) {
                let x = x - 1;
            }
            if(Controller.right()) {
                let x = x + 1;
            }
            if(Controller.up()) {
                let y = y - 1;   
            }
            if(Controller.down()) {
                let y = y + 1;
            }

            if (x = -1) { let x = 0; }
            if (x = 30) { let x = 29;}
            if (y = -1) { let y = 0; }
            if (y = 30) { let y = 29;}

            do Drawer.draw(editorCursor, 6, 6, x * 6, y * 6);

            if (Helpers.or(~(_x = x), ~(_y = y))) {
                if (gameField.isAliveAt(_x, _y)) {
                    do gameField.set(true, _x, _y);
                } else {
                    do gameField.set(false, _x, _y);
                }
                let _x = x;
                let _y = y;
                do Sys.wait(100);
            }

            if (toggle) {
                if (gameField.isAliveAt(x, y)) {
                    do gameField.set(false, x, y);
                } else {
                    do gameField.set(true, x, y);
                }
                do Sys.wait(100);
            }
        }
        do editorCursor.dispose();
        return;
    }
}